# Block 9 - Fold

## Programm

Im diesem letzten Block werden wir uns mit der ultimativen From von Abstraktion und Auslagerung beschäftigen. Bis jetzt konnte alles vereinfacht werden, bis auf den rekurviven Aufruf der Funktionen. Diesen Teil werden wir nun auch noch auslagern. Dazu werden wir uns die Funktion `fold` ansehen.

Wie immer zuerst die Signatur und die Testfälle:

```racket
(: list-fold (%b (%a %b -> %b) (list-of %a) -> %b))

(check-expect (list-fold 0 + (list 1 2 3 4 5)) 15)
(check-expect (list-fold 1 * (list 1 2 3 4 5)) 120)
```

Die Signatur sieh unheimlich kompliziert aus, ist aber eigentlich ganz einfach. `list-fold` nimmt 3 Parameter. Der erste Parameter ist ein normaler generischer Typ und wird vom dem ausgezeichnet, was weitergegeben wird. Der zweite Parameter ist eine Funktion, die den dritten Parameter und ersten Parameter nutzt und einen rekursiven Aufruf macht. Der dritte Parameter ist eine Liste. Der Rückgabewert ist der gleiche Typ wie der erste Parameter, damit immer der gleiche Typ weitergegeben wird.
z.B:

```racket
(: list-fold (Number (Number Number -> Number) (list-of Number) -> Number))
```

Die Definition von der Funktion ist nicht weniger kompliziert, also die Signatur. Der erste Parameter definiert, was passiert, wenn die Liste leer ist. Der zweite Parameter definiert, was generell - also wenn die Liste nicht leer ist - passiert. Der dritte Parameter ist die Liste selbst.

```racket
(define list-fold
  (lambda (for-empty for-cons list)
    (...)))
```

Nun machen wir die typische Fallunterscheidung. Wenn die Liste leer ist, dann soll `for-empty` ausgeführt werden. Ansonsten soll `for-cons` ausgeführt werden.

```racket
(define list-fold
  (lambda (for-empty for-cons list)
    (cond
      ((empty? list) for-empty) ; 0, 1 oder was ganz anderes
      ((cons? list) ...))))
```

Nun müssen wir uns überlegen, was `for-cons` machen soll. `for-cons` ist wie das normale `cons` in den anderen Funktionen. Es nimmt das erste Element der Liste und gibt den Rest per Rekursion an sich selbst weiter.

```racket
(define list-fold
  (lambda (for-empty for-cons list) ; Die neuen Variablen müssen den Parameter hinzugefügt werden!
    (cond
      ((empty? list) for-empty) ; 0, 1 oder was ganz anderes
      ((cons? list)
       (for-cons (first list)
                 (list-fold for-empty for-cons (rest list)))))))
```

So wird die ganze Rekursion der z.B. `map` Funktion in die `fold` Funktion ausgelagert. Zu diesem Codestück gibt es (meines Wissens nach) keine alternative aus anderen Programmiersprachen. Das ist einzigartig in Racket. Das ist auch der Grund, warum `fold` so wichtig ist. Es ist die ultimative Form der Abstraktion und Auslagerung.

Jetzt schreiben wir die `list-map` und `list-filter` Funktionen mit `fold`.

### Filter

Die Signatur bleibt gleich, nur die Implementierung ändert sich.

```racket
(: list-filter ((%a -> boolean) (list-of %a) -> (list-of %a)))

; Testfälle
(check-expect (list-filter even? (list 1 2 3 4)) (list 2 4))
(check-expect (list-filter even? (list 5 6 7 8)) (list 6 8))
```

In der Implementierung wird die Rekursion durch `fold` ersetzt. Die Frage ist, was soll man für `for-empty` und `for-cons` eingeben. `for-empty` ist einfach, dass das passiert, wenn die Liste leer ist. In dem Fall, eine leere Liste. Das `for-cons` ist etwas komplizierter, denn man man muss einen Lambda-Ausdruck schreiben, der zwei Parameter, first & rest, hat. In der Mitte wird die Bedingung geschrieben, bei ja wird das aktuelle Element an die Liste angehängt, bei nein wird es übersprungen.

```racket
(define list-filter
  (lambda (f list)
    (list-fold empty
               (lambda
                 (first rest) ; erstes Element
                 (if (f first) ; Rest (mit rekursivem Aufruf -> in list-fold)
                   (cons first rest) ; erstelle neue (gefilterte) Liste und gibt diese in den rekursiven Aufruf
                   rest)) ; rekursiver Aufruf mit nächsten Element
               list)))
```

Dadurch, dass die Rekursion ausgelagert wurde, müssen die zwischen Definitionen nicht mehr gemacht werden.

### Map

Hier ändert sich auch nichts an der Signatur.

```racket
(: list-map ((%a -> %b) (list-of %a) -> (list-of %b)))

; Testfälle
(check-expect (list-map (lambda (x) (+ 1 x)) (list 1 2 3 4 5)) (list 2 3 4 5 6))
(check-expect (list-map (lambda (x) (+ 1 x)) (list 5 6 7 8 9)) (list 6 7 8 9 10))
```

Die Implementierung ist fast gleich wie bei `list-filter`. Der einzige Unterschied ist, dass die Funktion direkt das erste Element wirkt (und die Liste neu gemacht wird).

```racket
(define list-map
  (lambda (f list)
    (list-fold empty
               (lambda
                 (first rest)
                 (cons (f first) rest)) ; mapping Funktion anwenden und rekrusiven Aufruf starten
               list)))
```

## Aufgaben

In den folgenden Aufgaben werden alte Funktionen mit `fold` neu geschrieben.

1. Fold [Link](../Tasks/20_Fold.md)
