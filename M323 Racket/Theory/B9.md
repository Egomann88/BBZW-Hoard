# Block 9 - Fold

## Programm

Im letzten Block werden wir uns mit der ultimativen Form von Abstraktion und Auslagerung beschäftigen. Bis jetzt konnte alles vereinfacht werden, bis auf den rekursiven Aufruf der Funktionen. Diesen Teil werden wir nun auch auslagern und uns dazu die Funktion `fold` ansehen.

Wie immer zuerst die Signatur und die Testfälle:

```racket
(: list-fold (%b (%a %b -> %b) (list-of %a) -> %b))

(check-expect (list-fold 0 + (list 1 2 3 4 5)) 15)
(check-expect (list-fold 1 * (list 1 2 3 4 5)) 120)
```

Die Signatur sieh unheimlich kompliziert aus, ist aber ganz einfach. `list-fold` nimmt 3 Parameter. Der erste Parameter ist ein generischer Typ und wird vom weitergegebenen Wert bestimmt. Der zweite Parameter ist eine Funktion, die den dritten Parameter und den ersten Parameter nutzt und einen rekursiven Aufruf macht. Der dritte Parameter ist eine Liste. Der Rückgabewert ist der gleiche Typ wie der erste Parameter, damit immer der gleiche Typ weitergegeben wird.
z.B:

```racket
(: list-fold (Number (Number Number -> Number) (list-of Number) -> Number))
```

Die Definition der Funktion ist simpler als die Signatur. Der erste Parameter definiert, was passiert, wenn die Liste leer ist. Der zweite Parameter definiert, was generell - also wenn die Liste nicht leer ist - passiert. Der dritte Parameter ist die Liste selbst.

```racket
(define list-fold
  (lambda (for-empty for-cons list)
    (...)))
```

Nun machen wir die typische Fallunterscheidung. Wenn die Liste leer ist, dann wird `for-empty` ausgeführt. Ansonsten wird `for-cons` ausgeführt.

```racket
(define list-fold
  (lambda (for-empty for-cons list)
    (cond
      ((empty? list) for-empty) ; 0, 1 oder was ganz anderes
      ((cons? list) ...))))
```

Nun müssen wir uns überlegen, was `for-cons` machen soll. `for-cons` ist ähnlich wie das normale `cons` in den anderen Funktionen. Es nimmt das erste Element der Liste und gibt den Rest per Rekursion an sich selbst weiter.

```racket
(define list-fold
  (lambda (for-empty for-cons list) ; Die neuen Variablen müssen den Parameter hinzugefügt werden!
    (cond
      ((empty? list) for-empty) ; 0, 1 oder was ganz anderes
      ((cons? list)
       (for-cons (first list)
                 (list-fold for-empty for-cons (rest list)))))))
```

So wird die gesamte Rekursion der z.B. `map` Funktion in die `fold` Funktion ausgelagert. Zu diesem Codestück gibt es (meines Wissens nach) keine Alternative aus anderen Programmiersprachen. Das ist einzigartig in Racket und der Grund, warum `fold` wichtig ist. Es ist die ultimative Form der Abstraktion und Auslagerung.

Jetzt schreiben wir die `list-map` und `list-filter` Funktionen mit `fold`.

### Filter

Die Signatur bleibt gleich, nur die Implementierung ändert sich.

```racket
(: list-filter ((%a -> boolean) (list-of %a) -> (list-of %a)))

; Testfälle
(check-expect (list-filter even? (list 1 2 3 4)) (list 2 4))
(check-expect (list-filter even? (list 5 6 7 8)) (list 6 8))
```

In der Implementierung wird die Rekursion durch `fold` ersetzt. Die Frage ist, was für `for-empty` und `for-cons` eingeben werden soll. `for-empty` ist einfach: Es tritt auf, wenn die Liste leer ist, und gibt dann eine leere Liste zurück. `for-cons` ist etwas komplizierter, denn ein Lambda-Ausdruck muss geschrieben werden, der zwei Parameter, `first` & `rest`, hat. In der Mitte wird die Bedingung geschrieben: Wenn ja, wird das aktuelle Element an die Liste angehängt, wenn nein, wird es übersprungen.

```racket
(define list-filter
  (lambda (f list)
    (list-fold empty
               (lambda
                 (first rest) ; erstes Element
                 (if (f first) ; Rest (mit rekursivem Aufruf -> in list-fold)
                   (cons first rest) ; erstelle neue (gefilterte) Liste und gibt diese in den rekursiven Aufruf
                   rest)) ; rekursiver Aufruf mit nächsten Element
               list)))
```

Dadurch, dass die Rekursion ausgelagert wurde, müssen die Zwischendefinitionen nicht mehr gemacht werden.

### Map

Hier ändert sich auch nichts an der Signatur.

```racket
(: list-map ((%a -> %b) (list-of %a) -> (list-of %b)))

; Testfälle
(check-expect (list-map (lambda (x) (+ 1 x)) (list 1 2 3 4 5)) (list 2 3 4 5 6))
(check-expect (list-map (lambda (x) (+ 1 x)) (list 5 6 7 8 9)) (list 6 7 8 9 10))
```

Die Implementierung ist fast identisch mit der von `list-filter`. Der einzige Unterschied besteht darin, dass die Funktion direkt auf das erste Element wirkt (und die Liste neu erstellt wird).

```racket
(define list-map
  (lambda (f list)
    (list-fold empty
               (lambda
                 (first rest)
                 (cons (f first) rest)) ; mapping Funktion anwenden und rekrusiven Aufruf starten
               list)))
```

## Aufgaben

In den folgenden Aufgaben werden alte Funktionen mit `fold` neu geschrieben.

1. Fold [Link](../Tasks/20_Fold.md)
